// It's not required to use ForgeGradle because re-obfuscation is not required for this.
// Re-obfuscation (reobf task) will work and required for minecraft's identifiers
// but this project doesn't use minecraft's classes so ForgeGradle is not required

apply plugin: 'java'

version = "1.0.0"
group = "zone.rong.nukejndilookupfromlog4j"
archivesBaseName = "nukejndilookupfromlog4j"

sourceCompatibility = targetCompatibility = '1.8'

repositories {
    maven { url = "https://files.minecraftforge.net/maven" }
    maven { url = "https://libraries.minecraft.net/" }
}

sourceSets {
    forge1122
    forge1710
}

dependencies {
    // common dependencies
    //noinspection GradlePackageUpdate // suppress IDEA
    implementation "org.apache.logging.log4j:log4j-api:2.8.1"
    //noinspection GradlePackageUpdate // suppress IDEA
    implementation "org.apache.logging.log4j:log4j-core:2.8.1"
    implementation "net.minecraft:launchwrapper:1.12"

    // the dependency for forge 1.12.2 part
    forge1122Implementation "net.minecraftforge:forge:1.12.2-14.23.5.2856:universal"
    //noinspection GradlePackageUpdate // suppress IDEA
    forge1122Implementation "com.google.guava:guava:21.0"

    // the dependency for forge 1.7.10 part
    forge1710Implementation "net.minecraftforge:forge:1.7.10-10.13.4.1614-1.7.10:universal"
    //noinspection GradlePackageUpdate // suppress IDEA
    forge1710Implementation "com.google.guava:guava:17.0"
}

jar {
    from sourceSets.forge1122.output
    from sourceSets.forge1710.output

    manifest {
        attributes([
                "TweakClass": 'zone.rong.nukejndilookupfromlog4j.tweaker.NukeJndiLookupFromLog4j',
        ])
    }
}
